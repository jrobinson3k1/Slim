apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.metova'
version = '2.0.0-beta.1'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 15

        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    compile project(':library-annotation')
    compile 'com.android.support:appcompat-v7:25.0.1'
    testCompile 'junit:junit:4.12'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                artifactId 'slim'
                packaging 'aar'

                name 'Slim'
                url 'https://github.com/metova/slim'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'jrobinson3k1'
                        name 'Jason Robinson'
                        email 'jason.robinson@metova.com'
                    }
                }
                scm {
                    connection 'https://github.com/metova/slim.git'
                    developerConnection 'https://github.com/metova/slim.git'
                    url 'https://github.com/metova/slim'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}